000010 REM Programm zur Übernahme der Trägerdaten
000020 REM 27.04.2009 - Schmidt (Programmerstellung)
000030 REM 11.08.2009 - Schmidt (Handling 2. Empfänger)
000040 
000050      INCLUDE "CGI"
000060      INCLUDE "EMAIL" ! Email Toolkit
000070      INCLUDE "WFALLGEM.INC" ! allgemeine Funktionen
000080      INCLUDE "WFKIFEHL.INC" ! Fehlerbehandlung
000090      INCLUDE "WFFOOTER.INC" ! Fußzeile
000100      INCLUDE "WFSESSION.INC" ! Sessionmanagement
000110 
000120      OPTION VERSION 1.0.03,"Mitttermeier & Ungelenk GmbH"
000130 
000140      ON ERROR GOTO ERROR.ROUT
000150 
000160 REM *******************************************************
000170      CALL CGI.INIT
000180      CALL CGI.GET("kdnr",ADDROF(KDNR$)) ! Kundennummer übernehmen
000190      CALL CGI.GET("sid",ADDROF(SID$))
000200      CALL CGI.GET("aktion",ADDROF(AKTION$))
000210 REM *******************************************************
000220      CALL CGI.GET("nname",ADDROF(TRAENAME$))
000230      CALL CGI.GET("vname",ADDROF(TRAEVNAME$))
000240      CALL CGI.GET("schrank",ADDROF(TRAESCHR$))
000250      CALL CGI.GET("fach",ADDROF(TRAEFAC$))
000260      CALL CGI.GET("groesse",ADDROF(TRAEGRO$))
000270      CALL CGI.GET("kst",ADDROF(TRAEKST$))
000280      CALL CGI.GET("info",ADDROF(TRAEINFO$))
000290 
000300      TRAENAME$ = FN.UMLAUTE.ZU.THEOS$(TRAENAME$)
000310      TRAEVNAME$ = FN.UMLAUTE.ZU.THEOS$(TRAEVNAME$)
000320      TRAEINFO$ = FN.UMLAUTE.ZU.THEOS$(TRAEINFO$)
000330 
000340 REM *******************************************************
000350 REM ************ Session prüfen ***************
000360      FKDNR$ = FORMAT$(VAL(KDNR$),"999999")
000370      RC$ = FN.WFSESSION$("session.update",SID$,FKDNR$)
000380 REM *******************************************
000390      NAVIGATION$ = "<a href=""javascript:document.home.submit();""><img src=""../../global/pics/home.png"" border=""0""><font face=""verdana"" size=""2"">Startseite </font></a>"
000400      NAVIGATION2$ = "<a href=""javascript:document.bk_idx.submit();""><img src=""../../global/pics/bk.png"" border=""0""><font face=""verdana"" size=""2"">zur&uuml;ck zu Tr&auml;ger </font></a>"
000410      NAVIGATION.FORM$ = "<form action=""/cgi.opbk/home"" name=""home"" method=""post""><input type=""hidden"" name=""kdnr"" value="""&KDNR$&"""><input type=""hidden"" name=""sid"" value="""&SID$&"""></form>"
000420      NAVIGATION.FORM2$ = "<form action=""/cgi.opbk/bk_idx"" name=""bk_idx"" method=""post""><input type=""hidden"" name=""kdnr"" value="""&KDNR$&"""><input type=""hidden"" name=""sid"" value="""&SID$&"""></form>"
000430      SELECT AKTION$
000440           CASE "Absenden"
000450                FL% = 3 \ OPEN #3:"/WWWROOT/OPBK/PARAMET.OPBK", INPUT INDEXED ! Parameter
000460                FL% = 4 \ OPEN #4:"/WWWROOT/EXPORT/TRAEEXP.OPBK", OUTPUT INDEXED ! Exportdatei
000470                FL% = 5 \ OPEN #5:"/WWWROOT/OPBK/STAMMDA.OPBK", UPDATE INDEXED 
000480                KY3$ = "1"&FKDNR$
000490                KY5$ = "1TAN"&FKDNR$
000500                GOSUB L5
000510                GOSUB L3 ! Parameter lesen
000520                IF TANMERK>=900000.0 THEN TANMERK = 0
000530                TANMERK = TANMERK+1
000540                KY4$ = "1TAN"&FKDNR$&FORMAT$(TANMERK,"999999")
000550                IF TRIM$(TRAEINFO$)="" THEN TRAEINFO$ = "keine Anmerkung"
000560                WRITE #4,KY4$:FKDNR$,TRAENAME$,TRAEVNAME$,TRAESCHR$,TRAEFAC$,TRAEGRO$,TRAEKST$,TRAEINFO$,RES$,RES$,RES,RES,RES,RES%,RES%,RES%,DATE$(0),TIME$(0)
000570                KY5$ = "1TAN"&FKDNR$
000580                GOSUB S5 !
000590                CLOSE #3 \ CLOSE #4 \ CLOSE #5
000600                FL% = 11 \ OPEN #11:"/FISTAM.REI", INPUT INDEXED 
000610                KY11$ = "101"
000620                FL% = 11 \ READ #11,KY11$:ZNR%,ZA1$,ZA2$,ZLA$,ZST$,ZPL,ZOR$,ZTE$,ZTX$,ZTXX$,TEMP$,TEMP$,ZF100$,ZF101$,ZF102$,ZF103$,ZF104$,ZF105$,ZF106$ \ UNLOCK #11 \ CLEAR FL%
000630                KY11$ = "1KISS"
000640                FL% = 11 \ READ #11,KY11$:MAX.USER$,KIP$,KUS$,KPW$,MIP$,MUS$,MPAS$,MPORT$ \ UNLOCK #98 \ CLEAR FL%
000650                CLOSE #11
000660                IF EMAIL%=1 AND ZF105$<>"" ! Email Protokoll an Wäscherei
000670                     FL% = 10 \ OPEN #10:"/KUSTWA.REI", INPUT INDEXED 
000680                     FL% = 12 \ OPEN #12:"/WWWROOT/OPBK/SETTING.OPBK", INPUT INDEXED 
000690                     KY10$ = "1"&FKDNR$
000700                     KY12$ = "1"&FKDNR$
000710                     GOSUB L10 ! Kundenstamm Wäscherei lesen
000720                     GOSUB L12 ! Will der Kunde ein eMail - Protokoll?
000730                     CLOSE #10 \ CLOSE #12
000740                     FL% = 2 \ OPEN #2:SID$&".email:m", OUTPUT SEQUENTIAL 
000750                     TRENNER% = SCH(1,ZF105$,";")
000760                     IF TRENNER%>0
000770                          EMPF$(1) = ZF105$[1:TRENNER%-1] ! Empfänger 1
000780                          EMPF$(2) = ZF105$[TRENNER%+1:LEN(ZF105$)] ! Empfänger 2
000790                     ELSE ! kein 2. Empfänger
000800                          EMPF$(1) = TRIM$(ZF105$)
000810                          IFEND 
000820                     PRINT #2:"To: ";TRIM$(LCASE$(EMPF$(1)))
000830                     IF VAL(PROTOKOLL$)=1 ! Kundenprotokoll
000840                          PRINT #2:"Cc: ";TRIM$(LCASE$(EMAIL$))
000850                          IFEND 
000860                     IF TRIM$(EMPF$(2))<>"" ! 2. Empfänder als Blind-Copy
000870                          PRINT #2:"Bcc: ";TRIM$(LCASE$(EMPF$(2)))
000880                          IFEND 
000890                     PRINT #2:"From: ";TRIM$(LCASE$(ZF106$))
000900                     PRINT #2:"Subject: neuer Träger angelegt "&DATE$(0)&"-"&TIME$(0)
000910                     PRINT #2:""
000920                     PRINT #2:"Kundennummer "&FKDNR$&" ("&TRIM$(KA1$)&","&TRIM$(KA2$)&") hat einen neuen Träger angelegt"
000930                     PRINT #2
000940                     MAIL.ZEILE$ = "Nachname......: "
000950                     PRINT #2:MAIL.ZEILE$&TRAENAME$&CHR$(10);
000960                     MAIL.ZEILE$ = "Vorname.......: "
000970                     PRINT #2:MAIL.ZEILE$&TRAEVNAME$&CHR$(10);
000980                     MAIL.ZEILE$ = "Schrank.......: "
000990                     PRINT #2:MAIL.ZEILE$&TRAESCHR$&CHR$(10);
001000                     MAIL.ZEILE$ = "Fach..........: "
001010                     PRINT #2:MAIL.ZEILE$&TRAEFAC$&CHR$(10);
001020                     MAIL.ZEILE$ = "Größe.........: "
001030                     PRINT #2:MAIL.ZEILE$&TRAEGRO$&CHR$(10);
001040                     MAIL.ZEILE$ = "Kostenstelle..: "
001050                     PRINT #2:MAIL.ZEILE$&TRAEKST$&CHR$(10);
001060                     MAIL.ZEILE$ = "Anmerkung.....: "
001070                     PRINT #2:MAIL.ZEILE$&TRAEINFO$&CHR$(10);
001080                     PRINT #2
001090                     PRINT #2:DATE$(0)&" - "&TIME$(0)
001100                     PRINT #2
001110                     CLOSE #2 \ CLEAR FL%
001120                     CALL EMAIL.PROFILEEX("",TRIM$(MIP$),TRIM$(MUS$),TRIM$(MPAS$),"","","","",STR$(VAL(MPORT$)),"")
001130                     CALL EMAIL.SEND(SID$&".email:m",0)
001140                     RC = CALL.RETURN.VALUE
001150                     IF CALL.RETURN.VALUE>0
001160                          CALL EMAIL.ERROR.MSG(RC,ADDROF(CRC$))
001170                          GOTO ERROR.ROUT
001180                          IFEND 
001190                     SYSTEM "er "&SID$&".email:m (NOQ"
001200                     IFEND 
001210                MSG$(1) = "Daten wurden erfolgreich gespeichert!"
001220                MSG$(2) = "<br />Es wird ein Mitarbeiter der W&auml;scherei benachrichtigt um die Daten zu &uuml;berpr&uuml;fen."
001230           OTHERWISE 
001240                PRINT "Hacking attempt!" \ STOP 
001250           CEND 
001260      CALL CGI.TEMPLATE.VAR("kdnr",KDNR$)
001270      CALL CGI.TEMPLATE.VAR("msg1",MSG$(1))
001280      CALL CGI.TEMPLATE.VAR("msg2",MSG$(2))
001290      CALL CGI.TEMPLATE.VAR("msg3",MSG$(3))
001300      CALL CGI.TEMPLATE.VAR("navigation",NAVIGATION$)
001310      CALL CGI.TEMPLATE.VAR("navigation.form",NAVIGATION.FORM$)
001320      CALL CGI.TEMPLATE.VAR("navigation2",NAVIGATION2$)
001330      CALL CGI.TEMPLATE.VAR("navigation.form2",NAVIGATION.FORM2$)
001340      RC$ = FN.WFFOOTER$(NIX$) ! fußzeile
001350      CALL CGI.TEMPLATE("/wwwroot/opbk/tpl/hinweis.tpl")
001360 ENDE:
001370      END \ QUIT 
001380 REM ------------------------------------------------
001390 ERROR.ROUT:
001400      RC$ = FN.WFKIFEHL$("bk_an",ERR,ERL,CRC$,FL%)
001410 
001420 REM --------------------------------------------------------
001430 L3: ! OPA Parameter lesen
001440      FL% = 3 \ READ #3,KY3$:XKNR$,PW$,TRAEST%,TRAEAR%,TRAEAN%,TRAELI%,TRAEBE%,TEMP%,TEMP%,EMAIL% \ UNLOCK #3 \ CLEAR FL%
001450      RETURN 
001460 REM --------------------------------------------------------
001470 L5: ! OPBK Firemstamm lesen
001480      FL% = 5 \ READ #5,KY5$:XX$,TANMERK \ CLEAR FL%
001490      RETURN 
001500 REM --------------------------------------------------------
001510 S5: ! OPBK Firmenstamm schreiben
001520      FL% = 5 \ WRITE #5,KY5$:FKDNR$,TANMERK,DATE$(0),TIME$(0) \ CLEAR FL%
001530      RETURN 
001540 REM --------------------------------------------------------
001550 L10: ! Kundenstamm lesen
001560      FL% = 10 \ READ #10,KY10$:K8%,K9%,KF%,KNR,KA1$,KA2$,KST$,KPL,KOR$,KL$,KSW$,KI$,KUG$,KDB,K1%,K2%,KRB$,KFA%,KTU%,KGR%,KBK%,KAN%,KMG%,KBZ%,KLB$,K3%,KPO%,KOP%,KT$,K4%,K5%,XX1$,XX2$,FK1% \ UNLOCK #10 \ CLEAR FL%
001570      RETURN 
001580 REM --------------------------------------------------------
001590 L12: ! persönliche Einstellungen  lesen
001600      READ #12,KY12$:EMAIL$,XX$,XX$,XX$,PROTOKOLL$,XX%,XX%,XX%
001610      RETURN 
