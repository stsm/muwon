000010 REM Programm zur Anzeige der bewo_la_aus.tpl Datei
000020 REM Ausgabe "letzte eingegangene Artikel"
000030 REM Schmidt - 08.01.2009 (Programmerstellung)
000040 
000050 REM *******************************************************
000060      INCLUDE "CGI" ! Toolkit : Common Gateway Interface
000070      INCLUDE "WFKIFEHL.INC" ! Fehlerbehandlung
000080      INCLUDE "WFFOOTER.INC" ! Fußzeile
000090      INCLUDE "WFSESSION.INC" ! Sessionmanagement
000100 
000110      OPTION VERSION 1.0.01,"Mittermeier & Ungelenk GmbH"
000120 
000130      ON ERROR GOTO ERROR.ROUT
000140 
000150 REM *******************************************************
000160      CALL CGI.INIT
000170      CALL CGI.GET("mita",ADDROF(MITA$)) ! Mitarbeiter
000180      CALL CGI.GET("kdnr",ADDROF(KDNR$)) ! Kundennummer übernehmen
000190      CALL CGI.GET("sid",ADDROF(SID$)) ! Session-ID
000200      CALL CGI.GET("aktion",ADDROF(AKTION$)) ! Aktion - was wollen wir
000210      CALL CGI.GET("methode",ADDROF(METHODE$)) ! BewohnernNr/Namenliste
000220      CALL CGI.GET("namen",ADDROF(GBEWONA$)) ! Bewohnernummer
000230      CALL CGI.GET("nummer",ADDROF(GBEWONR$)) ! Bewohnernummer
000240      CALL CGI.GET("suchna",ADDROF(GBEWOSU$)) ! Suchname
000250      CALL CGI.GET("gef.bewo",ADDROF(MAXBEWO$)) ! Anzahl der gef. Bewo.
000260      CALL CGI.GET("sort",ADDROF(SORT$)) ! Sortierungsparameter
000270 REM *******************************************************
000280 REM ************ Session prüfen ***************
000290      FKDNR$ = FORMAT$(VAL(KDNR$),"999999")
000300      RC$ = FN.WFSESSION$("session.update",SID$,MITA$)
000310 REM *******************************************
000320 
000330      DIM LFNR$(100),ARTBEZ$(100),BARCODE$(100),DATEIN$(100)
000340      DIM SORT%(100),ARTNR(100)
000350 
000360 REM *******************************************************
000370      IF METHODE$="namen" THEN EINGABE$ = GBEWONA$ ! ggf Übernahme
000380      IF METHODE$="nummer" THEN EINGABE$ = GBEWONR$ ! ggf. Übernahme
000390      IF METHODE$="suchna" THEN EINGABE$ = GBEWOSU$ ! ggf. Übernahme
000400      FONT$ = "<font color=""#000000"" face=""verdana"" size=""2"">" ! Schriftart
000410      FORMA.SORT$ = "<form action=""/cgi.ops/bewo_la_aus"" method=""post"">"
000420      SELECT.SORT$ = FN.FORM.SELECT$("sort",1)
000430      SELECT.SORT.INHALT$(1) = FN.FORM.SELECT.OPTION$("1",0,"Artikelnummer")
000440      SELECT.SORT.INHALT$(2) = FN.FORM.SELECT.OPTION$("2",0,"Bezeichnung")
000450      SELECT.SORT.INHALT$(3) = FN.FORM.SELECT.OPTION$("3",0,"Barcode")
000460      SELECT.SORT.INHALT$(4) = FN.FORM.SELECT.OPTION$("4",0,"Eingang")
000470      BUTTON.SORT$ = FN.FORM.BUTTON$("sort","Sortieren")
000480      FORME.SORT$ = "</form>"
000490      PARA1$ = FN.FORM.HIDDEN$("aktion","list")
000500      PARA2$ = FN.FORM.HIDDEN$("kdnr",KDNR$)
000510      PARA3$ = FN.FORM.HIDDEN$("sort",SORT$)
000520      PARA4$ = FN.FORM.HIDDEN$("nummer",EINGABE$)
000530      PARA5$ = FN.FORM.HIDDEN$("methode","nummer")
000540      PARA6$ = FN.FORM.HIDDEN$("sid",SID$)
000550      PARA7$ = FN.FORM.HIDDEN$("mita",MITA$)
000560      SELECT AKTION$
000570           CASE "list"
000580                IF VAL(EINGABE$)<=0
000590                     MSG$(1) = "<li>Die Bewohnernummer darf nicht kleiner/gleich Null sein!</li>"
000600                     GOTO FEHLER
000610                     IFEND 
000620                FL% = 21 \ OPEN #21:"/BEWO21.REI", INPUT INDEXED 
000630                FL% = 22 \ OPEN #22:"/BEWO22.REI", INPUT INDEXED 
000640                FL% = 23 \ OPEN #23:MITA$&"."&SID$&":m", OUTPUT SEQUENTIAL ! Ausgabedatei für PDF
000650                KY22$ = "1"&FORMAT$(VAL(KDNR$),"999999")&FORMAT$(VAL(EINGABE$),"999999")
000660                GOSUB L22 ! Leseanfang BEWO22.REI
000670                GOSUB L221 ! nächsten Satz BEWO22.REI
000680                IF EOF(22) OR KY22$[2:7]<>FORMAT$(VAL(KDNR$),"999999") OR KY22$[8:13]<>FORMAT$(VAL(EINGABE$),"999999") THEN GOTO 770 ! zweiter Durchlauf
000690                KY21$ = "1"&KY22$[20:31]
000700                GOSUB L21 ! Barcodebewohnerartikel BEWO21.REI
000710                IF EOF(21) THEN GOTO 670 ! nächsten Satz BEWO22
000720                TR54$ = TR54$[1:2]&"-"&TR54$[3:4]&"-20"&TR54$[5:6]
000730                IF LEIN<DAY(TR54$) THEN LEIN = DAY(TR54$)
000740                GOTO 670 ! nächster Satz BEWO22
000750                CLEAR TR54$
000760 
000770                KY22$ = "1"&FORMAT$(VAL(KDNR$),"999999")&FORMAT$(VAL(EINGABE$),"999999")
000780                GOSUB L22 ! Leseanfang BEWO22.REI
000790                GOSUB L221 ! nächsten SAtz BEWO22.REI
000800                IF EOF(22) OR KY22$[2:7]<>FORMAT$(VAL(KDNR$),"999999") OR KY22$[8:13]<>FORMAT$(VAL(EINGABE$),"999999") THEN GOTO 990 ! Seite generieren
000810                KY21$ = "1"&KY22$[20:31]
000820                GOSUB L21      ! Barcodebewohnerartikel BEWO21.REI
000830                TR54$ = TR54$[1:2]&"-"&TR54$[3:4]&"-20"&TR54$[5:6]
000840                IF DAY(TR54$)<LEIN THEN GOTO 790
000850                I% = I%+1
000860                LFNR$(I%) = STR$(I%)
000870                ARTBEZ$(I%) = TR64$
000880                ARTNR(I%) = TRAR
000890                BARCODE$(I%) = AKY21$[2:13]
000900                DATEIN$(I%) = STR$(DAY(TR54$))
000910                IF PDF.KOPF.MERKER%=0      ! Kopf das erste mal schreiben
000920                     GOSUB KOPF
000930                     PDF.KOPF.MERKER% = 1
000940                     IFEND 
000950                GOTO 790 ! nächster satz BEWO22
000960 
000970 ! *********** ABSCHLIESSENDE AKTIONEN VOR SEITENGENERIERUNG ***********
000980 
000990                IF I%=0 ! prüfen ob Bewohner Teile hat
001000                     MSG$(1) = "<li>Es wurden keine W&auml;scheteile f&uuml;r den Bewohner gefunden!</li>"
001010                     GOTO FEHLER
001020                     IFEND 
001030                SELECT SORT$
001040                     CASE "1" ! nach Artikelnummer sort.
001050                          MAT SORT% = SORT(ARTNR)
001060                     CASE "2" ! nach Bezeichnung sort.
001070                          MAT SORT% = SORT(ARTBEZ$)
001080                     CASE "3" ! nach Barcode sort.
001090                          MAT SORT% = SORT(BARCODE$)
001100                     CASE "4" ! nach Datum "Eingang" sort.
001110                          MAT SORT% = SORT(DATEIN$)
001120                     OTHERWISE 
001130                          PRINT "Jo - hier stimmt wat nich" \ STOP 
001140                     CEND 
001150                FOR SORTLAUF% = 1 TO 100 ! Sortierungslauf  [SORT%(100)]
001160                     IF STR$(ARTNR(SORT%(SORTLAUF%)))<>"0" ! alle 0 Felder nicht ausgeben
001170                          IF VAL(DATEIN$(SORT%(SORTLAUF%)))<=0 THEN DATEIN$(SORT%(SORTLAUF%)) = "999999"
001180                          HVAR% = HVAR%+1 ! Hilfsvariable für neue Pos.-nummer
001190                          CALL CGI.TEMPLATE.VAR("bewo.art"&STR$(HVAR%),"<tr><td align=""center"">"&FONT$&LFNR$(HVAR%)&"</td><td align=""center"">"&FONT$&STR$(ARTNR(SORT%(SORTLAUF%)))&"</td><td align=""left"">"&FONT$&ARTBEZ$(SORT%(SORTLAUF%))&"</td><td align=""center"">"&FONT$&BARCODE$(SORT%(SORTLAUF%))&"</td><td align=""center"">"&FONT$&DATE$(VAL(DATEIN$(SORT%(SORTLAUF%))))&"</td></tr>")
001200                          PDFZL$ = SPACE$(132)
001210                          PDFZL$[1:3] = LFNR$(HVAR%)
001220                          PDFZL$[5:10] = STR$(ARTNR(SORT%(SORTLAUF%)))
001230                          PDFZL$[13:38] = ARTBEZ$(SORT%(SORTLAUF%))
001240                          PDFZL$[40:52] = BARCODE$(SORT%(SORTLAUF%))
001250                          PDFZL$[54:61] = DATE$(VAL(DATEIN$(SORT%(SORTLAUF%))))
001260                          GOSUB PDF ! Zeile in Ausgabedatei für PDF schreiben
001270                          IFEND 
001280                     NEXT SORTLAUF%
001290                CLOSE #23
001300                SYSTEM "pdf "&MITA$&"."&SID$&":m > /WWWROOT/TEMP/"&SID$&".PDF:s (a4 landscape cols 132"
001310                SYSTEM "er "&MITA$&"."&SID$&" (noq" ! PDF-Arbeitsdatei löschen
001320                PDFDOWN$ = "/temp/"&SID$&".PDF"
001330                BEWONAME$ = TR63$
001340                MAXTEILE% = I%
001350                CALL CGI.TEMPLATE.VAR("kdnr",KDNR$)
001360                CALL CGI.TEMPLATE.VAR("pdf.down",PDFDOWN$)
001370                CALL CGI.TEMPLATE.VAR("bewo.name",BEWONAME$)
001380                CALL CGI.TEMPLATE.VAR("art.ges",STR$(MAXTEILE%))
001390                CALL CGI.TEMPLATE.VAR("bewo.nr",FORMAT$(VAL(EINGABE$),"999999"))
001400                CALL CGI.TEMPLATE.VAR("forma.sort",FORMA.SORT$)
001410                CALL CGI.TEMPLATE.VAR("select.sort",SELECT.SORT$)
001420                CALL CGI.TEMPLATE.VAR("select.sort.inhalt1",SELECT.SORT.INHALT$(1))
001430                CALL CGI.TEMPLATE.VAR("select.sort.inhalt2",SELECT.SORT.INHALT$(2))
001440                CALL CGI.TEMPLATE.VAR("select.sort.inhalt3",SELECT.SORT.INHALT$(3))
001450                CALL CGI.TEMPLATE.VAR("select.sort.inhalt4",SELECT.SORT.INHALT$(4))
001460                CALL CGI.TEMPLATE.VAR("button.sort",BUTTON.SORT$)
001470                CALL CGI.TEMPLATE.VAR("forme.sort",FORME.SORT$)
001480                CALL CGI.TEMPLATE.VAR("hilfe",HILFE$)
001490                CALL CGI.TEMPLATE.VAR("para1",PARA1$)
001500                CALL CGI.TEMPLATE.VAR("para2",PARA2$)
001510                CALL CGI.TEMPLATE.VAR("para3",PARA3$)
001520                CALL CGI.TEMPLATE.VAR("para4",PARA4$)
001530                CALL CGI.TEMPLATE.VAR("para5",PARA5$)
001540                CALL CGI.TEMPLATE.VAR("para6",PARA6$)
001550                CALL CGI.TEMPLATE.VAR("para7",PARA7$)
001560                CALL CGI.TEMPLATE.VAR("kdnr",KDNR$)
001570                CALL CGI.TEMPLATE.VAR("mita",MITA$)
001580                RC$ = FN.WFFOOTER$(NIX$)
001590                CALL CGI.TEMPLATE("/wwwroot/ops/tpl/bewo_la_aus.tpl")
001600                GOTO ENDE
001610           OTHERWISE 
001620                PRINT "Hacking attempt!"
001630                STOP 
001640           CEND 
001650 ! ********************* HAUPTPROGRAMM ENDE ***********************
001660 FEHLER:
001670      CALL CGI.TEMPLATE.VAR("msg1",MSG$(1))
001680      CALL CGI.TEMPLATE.VAR("msg2",MSG$(2))
001690      CALL CGI.TEMPLATE.VAR("msg3",MSG$(3))
001700      CALL CGI.TEMPLATE("/wwwroot/ops/tpl/fehler.tpl")
001710 ENDE:
001720      END \ QUIT 
001730 REM ------------------------------------------------
001740 ERROR.ROUT:
001750      RC$ = FN.WFKIFEHL$("bewo_la_aus",ERR,ERL,CRC$,FL%)
001760 
001770 REM *******************************************************
001780 KOPF: ! Kopfzeile für PDF ausgabe
001790      SZ% = SZ%+1 ! Seitenzähler
001800      PRINT #23:"L E T Z T E  E I N G E G A N G E N E  A R T I K E L"
001810      PRINT #23:"Bewohnername: "&TRIM$(TR63$);SPACE$(4);"Datum: "&DATE$(0);SPACE$(4);"Uhrzeit: "&TIME$(0);SPACE$(10)&"Seite: "&STR$(SZ%)
001820      PRINT #23
001830      PP% = PP%+1 ! Zeilenzähler
001840      PRINT #23:"Pos";TAB(5);"Art-Nr.";TAB(13);"Artikelbezeichnung";TAB(40);"Barcode";TAB(54);"Eingang"
001850      PRINT #23:RPT$(132,"-")
001860      RETURN 
001870 REM *******************************************************
001880 PDF: ! Dateiausgabe PDF
001890      PP% = PP%+1 ! Zeilenzähler
001900      PRINT #23:PDFZL$
001910      IF PP%=38 ! Seitenwechsel/Seitenumbruch
001920           PRINT #23:SPACE$(50);"*** Ü B E R T R A G ***"
001930           CLEAR PP%
001940           GOSUB KOPF
001950           IFEND 
001960      IF SORTLAUF%=100 ! letzter Lauf -ergo- Fußzeile
001970           PRINT #23:SPACE$(132)
001980           PRINT #23:LEGENDE.PDF$
001990           PRINT #23
002000           IFEND 
002010      RETURN 
002020 REM *******************************************************
002030 L21: ! Barcode Bewohnerartikel BEWO21.REI
002040      FL% = 21 \ READ #21,KY21$:AKY21$,TRKD,TRAR,TRBEWO,TR61$,TR62$,TR63$,TR64$,TR65$,TR66,TR67,TR68$,TR71,TR72$,TR73$,TR74%,TR75%,TR76%,TR77,TR41,TR42,TR43,TR44,TR45,TR51$,TR52,TR53,TR54$,TR55$,TR90,TR91,TR92,TR93,TR94%,TR95%,TR96%,TR97%,XX1$,XX2$ \ UNLOCK #21 \ CLEAR FL%
002050      RETURN 
002060 L22: ! Index Bewohnerartikel BEWO22.REI
002070      FL% = 22 \ READ #22,KY22$:AKY22$,XX1$,XX2$ \ UNLOCK #22 \ CLEAR FL%
002080      RETURN 
002090 L221:
002100      FL% = 22 \ READNEXT #22,KY22$:AKY22$,XX1$,XX2$ \ UNLOCK #22 \ CLEAR FL%
002110      RETURN 
